free c : channel.
(* 

A -> B : A, N_A
B -> S : B, N_B, { A, N_A }_Kbs
S -> A : N_B, { B, K_ab, N_A }_Kas, { A, K_ab, N_B }_Kbs
A -> B : { A, K_ab, N_B }_Kbs, { N_B }_Kab

Full agreement is wrong (the adversary can arrange so that A does not see
{ A, K_ab, N_B }_Kbs).

No agreement on N_A, but agreement on N_B and on the session key K_ab.

Attack of Syverson for evinj:endAparam(x) ==> evinj:beginAparam(x).
*)

type key.
type host.
type nonce.

fun nonce_to_bitstring(nonce):bitstring [data,typeConverter].

(* Table of host names/keys shared with the server 
   The constructor host maps keys to host names.
   The private destructor getkey maps host names to keys.

   The adversary can create any number of dishonest participants
   by generating a fresh key k and computing host(k) to get the 
   corresponding identity.
 *)

fun fhost(key):host.
reduc forall x:key; getkey(fhost(x)) = x [private].

(* Shared key cryptography *)

fun encrypt(bitstring,key):bitstring.
reduc forall x:bitstring, y:key; decrypt(encrypt(x,y),y) = x.

(* Secrecy assumptions *)

not attacker(new kas).

(* honestParticipants is a private channel on which we send the name of
   the honest participants; we use this information to test whether
   a participant is honest *)
free honestParticipants : channel [private].

free secretA, secretB : bitstring[private].
query attacker(secretA);
      attacker(secretB).

event endAparam(host).
event endBparam(host).
event beginAparam(host).
event beginBparam(host).
event endBkey(host, host, nonce, key).
event beginBkey(host, host, nonce, key).


query x:host; inj-event(endAparam(x)) ==> inj-event(beginAparam(x)).
query x:host; inj-event(endBparam(x)) ==> inj-event(beginBparam(x)).
query x:host, y:host, z:nonce, t:key; inj-event(endBkey(x,y,z,t)) ==> inj-event(beginBkey(x,y,z,t)).

(* Role of the initiator with identity A and key kas shared with S *)

let processInitiator(A:host, kas:key) =
	new Na : nonce; 
	out(c, (A, Na)); 
	in(c, (nb: nonce, m1:bitstring, m2:bitstring));
        let (b:host, kab:key, na2:nonce) = decrypt(m1, kas) in
	event beginBparam(b);
	event beginBkey(b, A, nb, kab);
        if na2 = Na then 
        out(c, (m2, encrypt(nonce_to_bitstring(nb), kab)));
	(* OK protocol finished 
	   If the interlocutor is honest, execute the events endAparam
           and send a test message to check that the key kab is secret *)
	in(honestParticipants, =b);
	event endAparam(A);
	out(c, encrypt(secretA, kab)).

(* Role of the responder with identity B and key kbs shared with S *)
                         
let processResponder(B:host, kbs:key) =
	in(c, (a:host, na:nonce)); 
	event beginAparam(a);
        new Nb : nonce; 
	out(c, (B, Nb, encrypt((a, na), kbs)));
	in(c, (m3:bitstring, m4:bitstring));
        let (=a, kab:key, =Nb) = decrypt(m3, kbs) in
        if nonce_to_bitstring(Nb) = decrypt(m4, kab) then
	(* OK protocol finished 
           If the interlocutor is honest, execute the events endBparam
           and endBkey, and send a test message to check that the key kab 
	   is secret *)
	in(honestParticipants, =a);
	event endBparam(B);
	event endBkey(B, a, Nb, kab);
	out(c, encrypt(secretB, kab)). 

(* Server *)

let processS = 
	in(c, (b:host, nb:nonce, m5:bitstring));
	let kbs2 = getkey(b) in
        let (a:host, na:nonce) = decrypt(m5,kbs2) in
        let kas2 = getkey(a) in
        new kab:key;
	out(c, (nb, encrypt((b, kab, na), kas2), encrypt((a, kab, nb), kbs2))).

(* Start process *)

process
(* First replication: create an unbounded number of honest principals *) 
( 
    !
    (* Create a principal of key kas shared with S and identity A *)
    new kas : key; let A = fhost(kas) in 
    (
       (* The attacker knows the host names (but not the secret keys) *)
       out(c, A) |
       (* This principal is honest: we send its name on channel
          "honestParticipants" to record that fact *)
       (!out(honestParticipants, A)) | 
       (* Launch an unbounded number of sessions of the initiator A *)
       (!processInitiator(A, kas)) | 
       (* Launch an unbounded number of sessions of the responder A *)
       (!processResponder(A, kas))
    )
)
|
(* Launch an unbounded number of sessions of the server *)
(!processS)
