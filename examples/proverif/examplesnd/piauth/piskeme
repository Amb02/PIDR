free c.
(* SKEME *)

(* Public key cryptography *)

fun pk/1.
fun encrypt/2.
reduc decrypt(encrypt(x,pk(y)),y) = x.

(* Hash function *)

fun hash/1.

(* Diffie-Hellman *)

fun f/2.
fun g/1.
equation f(x,g(y)) = f(y,g(x)).

(* Keyed hash function *)

fun keyhash/2.

authquery Bparam/1.
authquery Bkey/2.
authquery Aparam/1.
authquery Akey/2.

(* A *)

let processA = in(c, pkX);
		begin Bparam(pkX);
               new x; new Ka;
                out(c, (encrypt((pkA, Ka), pkX), g(x)));
                in(c, m);
	        let (m1, m2, m3) = m in
		let Kb = decrypt(m1, skA) in
                let K0 = hash((Ka, Kb)) in
		if m3 = keyhash((g(x), m2, pkX, pkA), K0) then
                 let sh = hash(f(x, m2)) in 
                 begin Bkey(pkX, sh);
                 out(c, keyhash((m2, g(x), pkA, pkX), K0));
		 (* OK *)
                 if pkX = pkB then end Aparam(pkA); end Akey(pkA, sh).

(* B *)

let processB = in(c, m);
               let (m1, m2) = m in
               let (pkAr, Ka) = decrypt(m1, skB) in
		begin Aparam(pkAr);
               new Kb;
               let K0 = hash((Ka, Kb)) in
	       new y;
               let sh = hash(f(y, m2)) in
	        begin Akey(pkAr, sh);
	       out(c, (encrypt(Kb, pkAr), g(y), keyhash((m2, g(y), pkB, pkAr), K0)));
               in(c, m3);
	       if m3 = keyhash((g(y), m2, pkAr, pkB), K0) then
               (* OK *)
		if pkAr = pkA then end Bparam(pkB); end Bkey(pkB, sh).


process 

new skA; let pkA = pk(skA) in
out(c, pkA);
new skB; let pkB = pk(skB) in
out(c, pkB);
new secretA;
new secretB;
((!processA) | (!processB))

