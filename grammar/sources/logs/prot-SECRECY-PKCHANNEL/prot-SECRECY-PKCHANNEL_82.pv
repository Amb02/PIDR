(* 

Certified email protocol by Abadi, Glew, Horne, and Pinkas
WWW2002

*)

(* set eqInNames = true. *)
set redundantHypElim = beginOnly.
set movenew = false.
set ignoreTypes = true.

type pkey.
type skey.
type spkey.
type sskey.
type key.
type tag.
type authtag.
type passwd.
type host.
type tquery.
type reply.
type messageid.
type tcertif.
type idttpr.

fun channel_for_host(host): channel [data, typeConverter].
fun channel_with_host(host, channel): channel [data].
fun id_for_host_ch(host, channel): idttpr [data].
fun id_for_key(key): idttpr [data].
fun id_for_host_key_key(host, key, key): idttpr [data].

(* Public key encryption *)

fun pk(skey):pkey.
fun A(pkey, bitstring): bitstring.
reduc forall y: skey, x: bitstring; decA(y, A(pk(y),x)) = x.

(* Signatures *)

fun S(sskey, bitstring): tcertif.
fun Spk(sskey): spkey.
reduc forall y: sskey, x: bitstring; checkS(Spk(y), S(y, x)) = x.
reduc forall y: sskey, x: bitstring; getS(S(y, x)) = x.

(* Shared key encryption *)

fun E(key, bitstring): bitstring.
reduc forall y: key, x: bitstring; decE(y, E(y,x)) = x.

(* Hash function *)

fun H(bitstring): key.

(* Constants to identify messages *)

const Give,      (* Message 1 *)
      Wants,     (* Message 2 *)
      Try,       (* Message 3 *)
      Released,  (* Message 4 *)
      Received: tag [data].  (* For the judge *)

(* Constant authentication modes 
   We encode the authentication modes as pairs:
     BothAuth in the paper is coded (Auth,Auth) 
     SAuth in the paper is coded (Auth,NoAuth)
     TTPAuth in the paper is coded (NoAuth,Auth)
     NoAuth in the paper is coded (NoAuth,NoAuth)
   This makes testing whether S / TTP must authenticate R easier *)

const Auth, NoAuth: authtag [data].

(* Function from R's password to R's name *)

fun PasswdTable(passwd): host.




(* It is assumed that an attacker cannot relate q and r = Reply(h,q) 
   except for the hosts h it creates itself *)

fun Reply(host, tquery): reply [private]. 
reduc forall x: passwd, q: tquery; ReplyOwnHost(x, q) = Reply(PasswdTable(x), q).



const NullQ: tquery [data].

reduc forall q: tquery; getAuthQ(q,NoAuth) = NullQ;
      forall q: tquery; getAuthQ(q,Auth) = q.

const NullR: reply [data].

reduc forall r: reply; getAuthR(r,NoAuth) = NullR;
      forall r: reply; getAuthR(r,Auth) = r.

const NullP: passwd [data].

reduc forall p: passwd; getAuthP(p,NoAuth) = NullP;
      forall p: passwd; getAuthP(p,Auth) = p.

(* Build a message *)

fun Message(host, messageid, bitstring): bitstring [private].

(* TTPname, Sname are public constants, representing the names
   of the hosts. We allow Sname to send to anybody, not just Rname.
   We also allow the TTP to talk to recipients other than Rname
   (represented by the adversary) on an insecure channel.
   cleartext is a public constant, header of the message from S to R *)	

free c: channel.
free cleartext: bitstring.
free Sname,Judgename,TTPname: host.
free TTPSigKey: sskey [private].
free TTPDecKey: skey [private].
free Rpwd: passwd [private].

(* Secrecy assumptions *)

not attacker(TTPDecKey).
not attacker(TTPSigKey).

(* Queries *)

event Rreceived(bitstring).
event JudgeSays(tag, host, bitstring).
event SThinksRhas(bitstring).
event Shas(host, key, bitstring, tquery, reply, bitstring).
event Rhas(idttpr, bitstring, key).
event TTPSendS(host, tcertif).
event TTPSendS2(host, tcertif).
event TTPSendR(idttpr, bitstring).
event TTPSendR2(idttpr, bitstring).






  (* For secrecy, we also want to know under which
     conditions the adversary can have the message sent by S
     to R, that is Message(PasswdTable(Rpwd),x,z).
   *)
  query x: messageid, z: bitstring; 
  	attacker(Message(PasswdTable(Rpwd),x,z)).


let processJudge(TTPEncKey: pkey, TTPVerKey: spkey) =
0.


let processS(TTPEncKey: pkey, TTPVerKey: spkey) = 
(* The attacker chooses possible recipients of the message *)
  in(c, recipient: host);
  (* The attacker chooses the authentication mode *)
  in(c, (sauth: authtag,ttpauth: authtag));
  (* Build the message to send *)
  new msgid: messageid;
  let m = Message(recipient,msgid,(sauth,ttpauth)) in
  (* Step 1 *)
  new k: key;
  new qtmp: tquery;
  let q = getAuthQ(qtmp,sauth) in
  let r = getAuthR(Reply(recipient,qtmp),sauth) in
  let em = E(k,m) in
  let hs = H((cleartext, ((q, r), em))) in
  let S2TTP = A(TTPEncKey, (Sname, ((sauth,ttpauth), (Give, ((k, recipient), hs))))) in
  event Shas(Sname, k, cleartext, q, r, m);
  out(channel_for_host(recipient), (TTPname, (((em, (sauth,ttpauth)), cleartext), (q, S2TTP))));

  (* Step 4 *)
  !
  in(channel_for_host(Sname), inmess4: tcertif);
  if ttpauth = Auth then
  (
     let (=Released, (=S2TTP, =recipient)) = checkS(TTPVerKey, inmess4) in
     (* S knows that R has read the message *)

     event SThinksRhas(m);
     0
     else out(channel_for_host(Sname), inmess4)
  ) else (
     let (=Released, =S2TTP) = checkS(TTPVerKey, inmess4) in 
     (* S knows that R has read the message, but cannot go to the judge,
        because in case S is dishonest, he could have queried TTP himself *)
     event SThinksRhas(m)
     else out(channel_for_host(Sname), inmess4)
  ).


let processR(Rname: host, RsecchannelToTTP: channel, RsecchannelFromTTP: channel, idTTPtoR: idttpr) = 
(* Step 2 *)
  in(channel_for_host(Rname), (=TTPname, (((em2: bitstring, (sauth: authtag,ttpauth: authtag)), cleartext2: bitstring), (q2: tquery, S2TTP2: bitstring))));
  let r2 = getAuthR(Reply(Rname,q2),sauth) in
  let hr = H((cleartext2, ((q2, r2), em2))) in
  event Rhas(idTTPtoR,em2,hr);

  out(RsecchannelToTTP, (S2TTP2, (Wants, (getAuthP(Rpwd,ttpauth), hr))));

  (* Step 3 *)
  !
  in(RsecchannelFromTTP, (=Try, (k3: key, =hr)));
  let m3 = decE(k3, em2) in
  (* R has obtained the message m3 = m *)
  event Rreceived(m3).


let processTTP(receivername: host, TTPsecchannelFromR: channel, TTPsecchannelToR: channel, idTTPtoR: idttpr) = 
  (* Step 3 *)
  in(TTPsecchannelFromR, (S2TTP3: bitstring, (=Wants, (RPwd3: passwd, hr3: key))));
  let (Sname3: host, ((sauth3: authtag,ttpauth3: authtag), (=Give, ((k3: key, R3: host), =hr3)))) = 
     decA(TTPDecKey, S2TTP3) in
  if R3 = receivername then
  (
     if (ttpauth3, R3) = (Auth, PasswdTable(RPwd3)) then

        (* Records the event *)
        event TTPSendS(Sname3,S(TTPSigKey, (Released, (S2TTP3, R3))));
        event TTPSendR(idTTPtoR, (Try, (k3, hr3)));

        (* Step 3 *)
        out(TTPsecchannelToR, (Try, (k3, hr3)));

        (* Step 4 *)
        out(channel_for_host(Sname3), S(TTPSigKey, (Released, (S2TTP3, R3))))

     else if ttpauth3 = NoAuth then

        (* Records the event *)
        event TTPSendS2(Sname3,S(TTPSigKey, (Released, S2TTP3)));
        event TTPSendR2(idTTPtoR, (Try, (k3, hr3)));

        (* Step 3 *)
        out(TTPsecchannelToR, (Try, (k3, hr3)));

        (* Step 4 *)
        out(channel_for_host(Sname3), S(TTPSigKey, (Released, S2TTP3)))
  ).


(* When establishing a connection, R always first sends its IP
   address. *)

(****** SECURE CHANNEL R-TTP BUILT WITH AN ASYMMETRIC CHANNEL ******)



(****** SECURE CHANNEL R-TTP BUILT WITH A SIMPLE PUBLIC KEY PROTOCOL ******)

const ChKey: tag [data].

let processRandTTP(Rname: host, TTPEncKey: pkey, TTPVerKey: spkey) = 
  (  !  
     (* R *)
     new shkey: key;
     out(channel_for_host(TTPname), Rname);
     out(channel_for_host(TTPname), A(TTPEncKey, (ChKey,shkey)));

     let idTTPtoR = id_for_key(shkey) in
     new RsecchannelToTTP: channel;
     new RsecchannelFromTTP: channel;

     (  !
        in(RsecchannelToTTP, m: bitstring);
	out(channel_for_host(TTPname),  E(shkey, m))
     )
     |
     (  !
        in(channel_for_host(Rname), mR: bitstring);
        out(RsecchannelFromTTP, decE(shkey, mR))
     )
     |
     processR(Rname, RsecchannelToTTP, RsecchannelFromTTP, idTTPtoR)
  )
  |
  (  !
     (* TTP *)
     in(channel_for_host(TTPname), receivername: host);
     in(channel_for_host(TTPname), m1: bitstring);
     let (=ChKey, shkey: key) = decA(TTPDecKey, m1) in
     let idTTPtoR = id_for_key(shkey) in
     new TTPsecchannelToR: channel;
     new TTPsecchannelFromR: channel;
     (  !
        in(TTPsecchannelToR, m: bitstring);
        out(channel_for_host(receivername),  E(shkey, m))
     )
     |
     (  !
        in(channel_for_host(TTPname), mTTP: bitstring); 
        out(TTPsecchannelFromR,decE(shkey, mTTP))
     )
     |
     processTTP(receivername, TTPsecchannelFromR, TTPsecchannelToR, idTTPtoR)
  ).



(****** SECURE CHANNEL R-TTP BUILT BY SSH ******)

(* In fact, we consider a simplified version of SSH, without negociation.
   We assume that the chosen parameters are 
   - the standard Diffie-Hellman key exchange
   - a symmetric encryption algorithm (different from none)
   - a mac algorithm (different from none)
   - no compression
   We do not consider key re-exchange.

   Only the transport layer protocol of SSH is considered. The 
   authentication is performed by the certified mail protocol,
   so we do not consider the SSH authentication protocol.

   In the computation of the mac, we ignore the sequence of the
   messages (difficult to model in our framework). Also note
   that the mac is really useful only when the encryption is malleable,
   and we model a perfect encryption that is not malleable.
 *)



(****** COMMON PART FOR ANY WAY OF BUILDING THE SECURE CHANNEL ******)

process 
        let TTPEncKey = pk(TTPDecKey) in
        out(c, TTPEncKey);
        let TTPVerKey = Spk(TTPSigKey) in
	out(c, TTPVerKey);
	let Rname = PasswdTable(Rpwd) in
	out(c, Rname);
        ((!processS(TTPEncKey, TTPVerKey)) | (!processJudge(TTPEncKey, TTPVerKey)) | processRandTTP(Rname, TTPEncKey, TTPVerKey))

  (* processRandTTP defines the two processes for R and TTP
     using processR and processTTP.

     It must define two channels RsecchannelToTTP and
     RsecchannelFromTTP above processR. processR uses these
     channels to communicate with TTP.

     Similarly, it must define two channels TTPsecchannelToR
     and TTPsecchannelFromR above processTTP, that processTTP
     uses to communicate with R. It must also define receivername,
     above processTTP. This identifier is used as address of the 
     receiver of the message TTP->R, and TTP will check that it is
     correctly set.
     
     At last, it must define idTTPtoR above both processTTP and
     processR. This identifier is used in events, to check that
     the channel TTP->R is the same from the point of views of 
     R and TTP.
  *)
